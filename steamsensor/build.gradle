buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
	}
}

apply plugin: 'java'
apply from: 'dependencies.gradle'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


repositories {
	flatDir {
		//used in distribution
		dirs 'lib'
	}
	google()
}

jar {
	baseName 'steamsensor'
	manifest {
		attributes 'Main-Class': 'com.thingworx.sdk.steam.SteamSensorClient'
	}
}

def useProjectIfPresent(notation, projectName) {
	if (project.hasProperty('sdkbuild')) {
		project(projectName)
	} else {
		dependencies.create(notation)
	}
}

dependencies {
	implementation useProjectIfPresent('com.thingworx:thingworx-java-sdk:+',':thingworx-java-sdk')
	implementation libs.javasdk
	implementation libs.jackson
	implementation libs.logging
	implementation 'commons-cli:commons-cli:1.3'
}

assemble.dependsOn(shadowJar)
jar.enabled = true

if (project.hasProperty('sdkbuild')){
	evaluationDependsOn(':thingworx-java-sdk')
}


